plugins {
    id 'org.jetbrains.intellij' version '1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21-2'
    id 'jacoco'

    id("io.gitlab.arturbosch.detekt") version '1.15.0'
}

group 'me.lensvol'
version '0.4.4'

compileKotlin {
    kotlinOptions.freeCompilerArgs = ['-Xjvm-default=enable']
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.moandjiezana.toml:toml4j:0.7.2"

    testImplementation('org.mock-server:mockserver-netty:5.3.0') {
        exclude group: 'ch.qos.logback'
    }

    implementation('io.sentry:sentry:1.7.30') {
        exclude group: 'org.slf4j'
    }

    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.15.0")
}

intellij {
    version.set(ideaVersion)
    updateSinceUntilBuild.set(false)
    pluginName.set('intellij-blackconnect')
    plugins.set(['PythonCore:211.6693.119'])
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

patchPluginXml {
    version.set(project.version)
    changeNotes.set("""
      <p>(UNRELEASED)</p>
      <ul>
        <li>Added "Trigger on Code Reformat" option</li>
        <li>Fix for rare crash when updating document during Undo/Redo operation.</li>
      </ul>

      <p>0.4.4</p>
      <br>
      <br>
      <p>This release is dedicated to the memory of our cat <b>Luna</b>, who passed away due to cancer last year.</p><br> 
      <p>She was kind, smart and loyal. Best cat in the world.</p><br>
      <p><b>We miss you, girl.</b></p>
      <br>
      <ul>
        <li>Add button to copy line length settings from the IDE ("right margin").</li>
        <li>Support for connecting to blackd over SSL (kudos to studioj)</li>
        <li>Make server error notifications more descriptive.</li>
        <li>Miscellaneous fixes and improvements.</li>
      </ul>

      <p>0.4.3</p>
      <ul>
        <li>Fix rare crash when processing source code with CR/LF sequence inside.</li>
        <li>Fix rare crash when saving files without type and Jupyter support is enabled.</li>
      </ul>

      <p>0.4.2</p>
      <ul>
        <li>Automatically close any outstanding error messages on successful call to blackd.</li>
        <li>Bump lower compatibility boundary to IDEA 2020.1.4 and up.</li>
      </ul>

      <p>0.4.1</p>
      <ul>
        <li>Fix regression with file save trigger reformatting non-Python files (reported by Matthew R. Scott).</li>
      </ul>

      <p>0.4.0</p>
      <ul>
        <li>Added "Check connection" button to "Settings" screen.</li> 
        <li>Added "Reformat Selected Fragment" action to support partial reformatting.</li>
        <li>Reformatting actions moved to <b>BlackConnect</b> submenu under "Tools".</li>
        <li>Fix rare crash when closing tab with window handle set to <i>null</i>.</li>
        <li>Notification balloons no longer show up in "Event log".</li>
      </ul>
      """)
}

processResources {
    def properties = ['version': project.version]
    inputs.properties(properties)
    filesMatching('version.properties') {
        expand(properties)
    }
}

publishPlugin {
    token.set(System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken"))
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

detekt {
    config = files("./detekt-config.yml")
    buildUponDefaultConfig = true // preconfigure defaults
    baseline = file("$projectDir/config/baseline.xml") // a way of suppressing issues before introducing detekt

    reports {
        html.enabled = false // observe findings in your browser with structure and code snippets
        xml.enabled = false // checkstyle like format mainly for integrations like Jenkins
        txt.enabled = false // similar to the console output, contains issue signature to manually edit baseline files
        sarif.enabled = false // SARIF integration (https://sarifweb.azurewebsites.net/) for integrations with Github
    }
}

tasks.detekt.jvmTarget = "1.8"
tasks.runPluginVerifier.ideVersions.set(["2020.1.4", "2020.2.3", "2020.3"])
